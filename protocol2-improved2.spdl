/* 
 *  A key exchange protocol
 *
 * improvements
 * - remove Nonce 'ni'
 * - have 'I' generate key 'kir'
 * - remove encryption with 'kir'
 * - reduce numberof args in 'hash'
 * - in msg 2: do not send both 'kir' and hash of 'kir'
 *
 */

// Hash function: nobody knows the inverse

hashfunction hash;

// User type declaration

usertype Key;

// Protocol description

protocol protocol2(I,R)
{
	role I
	{
		fresh kir: Key;
		var nr: Nonce;
		
		send_1 (I, R, { kir, I }pk(R) );
		
		recv_2 (R, I, { hash(kir, R), nr }pk(I) );

		send_3 (I, R, hash(nr) );


		claim_i1 (I, Secret, kir );
		claim_i2 (I, Nisynch );
	}

	role R
	{
		var kir: Key;
		fresh nr: Nonce;
		
		recv_1 (I, R, { kir, I }pk(R) );
		
		send_2 (R, I, { hash(kir, R), nr }pk(I) );
		
		recv_3 (I, R, hash(nr) );


		claim_r1 (R, Secret, kir );
		claim_r2 (R, Nisynch );
	}
}

